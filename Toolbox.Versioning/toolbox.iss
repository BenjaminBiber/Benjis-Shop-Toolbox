; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Shop-Toolbox"
#ifndef MyAppVersion
  #define MyAppVersion "0.0.0"
#endif
#ifndef MyPublishDir
  #define MyPublishDir "C:\\invalid\\publish"
#endif
#ifndef MyIconPath
  #define MyIconPath AddBackslash(MyPublishDir) + "wwwroot\\favicon.ico"
#endif
#define MyAppPublisher "4SELLERS GmbH"
#define MyAppURL "https://www.4sellers.de"
#define MyAppExeName "Toolbox.exe"
#define MyAppAssocName MyAppName + " File"
#define MyAppAssocExt ".myp"
#define MyAppAssocKey StringChange(MyAppAssocName, " ", "") + MyAppAssocExt
; Für Dateinamen Punkte/Leerzeichen entschärfen (optional)
#define MyAppVersionSafe StringChange(MyAppVersion, ".", "_")

[Setup]
AppId={{BC42C174-D98B-4F14-B518-52DF5E45B5A9}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DisableDirPage=yes
UninstallDisplayIcon={app}\{#MyAppExeName}
ArchitecturesAllowed=x64compatible
ArchitecturesInstallIn64BitMode=x64compatible
ChangesAssociations=yes
DisableProgramGroupPage=yes
;PrivilegesRequired=lowest
OutputDir=K:\Programme\Shop-Toolbox
; Version mit im Ausgabedateinamen
OutputBaseFilename=Installer_{#MyAppName}_{#MyAppVersionSafe}
SetupIconFile={#MyIconPath}
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "{#MyPublishDir}\\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs

[Registry]
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocExt}\OpenWithProgids"; ValueType: string; ValueName: "{#MyAppAssocKey}"; ValueData: ""; Flags: uninsdeletevalue
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}"; ValueType: string; ValueName: ""; ValueData: "{#MyAppAssocName}"; Flags: uninsdeletekey
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\{#MyAppExeName},0"
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1"""

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
; Start the application automatically after installation
; Install sqlcmd via winget if not present
Filename: "cmd.exe"; Parameters: "/c winget install --id Microsoft.sqlcmd --silent --accept-package-agreements --accept-source-agreements"; StatusMsg: "Installiere sqlcmd (falls nicht vorhanden)..."; Flags: runhidden; Check: NeedInstallSqlcmd
Filename: "{app}\\{#MyAppExeName}"; WorkingDir: "{app}"; Flags: nowait

[Code]
function IsSqlCmdInstalled(): Boolean;
var
  Code: Integer;
begin
  Code := -1;
  if Exec('cmd.exe', '/c where sqlcmd', '', SW_HIDE, ewWaitUntilTerminated, Code) then
    Result := (Code = 0)
  else
    Result := False;
end;

function WingetAvailable(): Boolean;
var
  Code: Integer;
begin
  Code := -1;
  if Exec('cmd.exe', '/c winget --version', '', SW_HIDE, ewWaitUntilTerminated, Code) then
    Result := (Code = 0)
  else
    Result := False;
end;

function NeedInstallSqlcmd(): Boolean;
begin
  Result := (not IsSqlCmdInstalled()) and WingetAvailable();
end;

